apply plugin: 'java'
apply plugin: 'maven'

apply plugin: 'checkstyle'
apply plugin: "findbugs"

apply plugin: 'eclipse'
apply plugin: 'idea'


archivesBaseName = 'clasrec-orchestrators'
version = '0.8-SNAPSHOT'
group = 'org.jlab.clas'

defaultTasks 'build'


configurations {
    provided
    compile.extendsFrom provided
}


repositories {
    mavenLocal()
    maven {
        url 'http://clasweb.jlab.org/clas12maven/'
    }
    mavenCentral()
}


dependencies {
    provided 'org.jlab.coda:jclara:4.3-SNAPSHOT'
    compile 'org.jlab.coda:jevio:4.4.6'
    compile 'org.yaml:snakeyaml:1.15'
    compile 'org.json:json:20160212'
    compile 'com.martiansoftware:jsap:2.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "org.mockito:mockito-core:1.+"
}



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar
    archives sourcesJar
}


if (!project.hasProperty("localPublicationRepo")) {
    ext.localPublicationRepo = "file://localhost/tmp/repo"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localPublicationRepo)
        }
    }
}


task distSources(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    classifier = 'src'
    compression = Compression.GZIP

    into "${baseName}-${version}-${classifier}"
    from "${projectDir}/build.gradle"
    from ("${projectDir}/config") {
        into 'config'
    }
    from ("${projectDir}/scripts") {
        into 'scripts'
    }
    from ("${projectDir}/src") {
        into 'src'
    }
    from "${projectDir}/services.yaml"
}


def deploySpec = copySpec {
    into ('plugins/clas12/lib') {
        from configurations.runtime - configurations.provided
        from "${jar.archivePath}"
    }

    into ('plugins/clas12/config') {
        from ("${projectDir}/config/services.yaml")
        from ("${projectDir}/config/files.list")
        from ("${projectDir}/config/dpe-clara.jsub")
        from ("${projectDir}/config/process-clara.jsub")
    }

    into ("bin") {
        from ("${projectDir}/scripts/local-orchestrator")
        from ("${projectDir}/scripts/cloud-orchestrator")
        fileMode 0755
    }
}


task deploy(type: Copy, dependsOn: jar) {
    def etc = new File("$System.env.CLARA_HOME/plugins/clas12/etc")
    def log = new File("$System.env.CLARA_HOME/plugins/clas12/log")
    def services = new File("$System.env.CLARA_HOME/plugins/clas12/services")
    etc.mkdirs()
    log.mkdirs()
    services.mkdirs()

    def dest = "$System.env.CLARA_HOME"
    into dest
    with deploySpec

    doFirst {
        if (dest == "null") {
            throw new GradleException('CLARA_HOME not set')
        }
    }
}



checkstyle {
    toolVersion = "6.14"
    configFile = new File(projectDir, "config/quality/checkstyle.xml")
    configProperties['samedir'] = new File(project.projectDir, "config/quality")
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
    effort = "default"
    reportLevel = "medium"
    excludeFilter = new File(projectDir, "config/quality/findbugs-exclude.xml")
}

task checkstyleHtml << {
    ant.xslt(in: checkstyleMain.reports.xml.destination,
             style: new File(projectDir, 'config/quality/checkstyle-noframes-sorted.xsl'),
             out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
}

checkstyleMain.finalizedBy checkstyleHtml

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// Marker task to enable findbugs.
task findbugs(
    group: "Verification",
    description: "Marker task to enabled findbugs."
)

gradle.taskGraph.whenReady { taskGraph ->
    tasks.findbugsMain.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
    tasks.findbugsTest.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
}



eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
    }
}
