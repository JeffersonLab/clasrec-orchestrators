apply plugin: 'java'
apply plugin: 'maven'

apply plugin: 'checkstyle'
apply plugin: 'findbugs'

apply plugin: 'eclipse'
apply plugin: 'idea'


group = 'org.jlab.clas'
archivesBaseName = 'clasrec-orchestrators'
version = '0.8-SNAPSHOT'

defaultTasks 'build'


configurations {
    provided
    compile.extendsFrom provided
}


repositories {
    mavenLocal()
    maven {
        url 'http://clasweb.jlab.org/clas12maven/'
    }
    mavenCentral()
}


dependencies {
    provided 'org.jlab.coda:jclara:4.3-SNAPSHOT'
    compile 'org.jlab.coda:jevio:4.4.6'
    compile 'org.yaml:snakeyaml:1.15'
    compile 'org.json:json:20160212'
    compile 'com.martiansoftware:jsap:2.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.+'
}



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar
    archives sourcesJar
}


if (!hasProperty('localPublicationRepo')) {
    ext.localPublicationRepo = 'file://localhost/tmp/repo'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localPublicationRepo)
        }
    }
}


task distSources(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    classifier = 'src'
    compression = Compression.GZIP

    into "${baseName}-${version}-${classifier}"
    from 'build.gradle'
    from ('config') {
        into 'config'
    }
    from ('scripts') {
        into 'scripts'
    }
    from ('src') {
        into 'src'
    }
    from 'services.yaml'
}


def deploySpec = copySpec {
    into ('plugins/clas12/lib') {
        from configurations.runtime - configurations.provided
        from jar.archivePath
    }

    into ('plugins/clas12/config') {
        from ('config/services.yaml')
        from ('config/files.list')
        from ('config/dpe-clara.jsub')
        from ('config/process-clara.jsub')
    }

    into ('bin') {
        from 'scripts/local-orchestrator'
        from 'scripts/cloud-orchestrator'
        fileMode 0755
    }
}


task deploy(type: Copy, dependsOn: jar) {
    def dest = "$System.env.CLARA_HOME"

    into dest
    with deploySpec

    doFirst {
        if (dest == "null") {
            throw new GradleException('CLARA_HOME not set')
        }
        file("${dest}/plugins/clas12/etc").mkdirs()
        file("${dest}/plugins/clas12/log").mkdirs()
        file("${dest}/plugins/clas12/services").mkdirs()
    }
}



checkstyle {
    toolVersion = '6.14'
    configFile = file('config/quality/checkstyle.xml')
    configProperties['samedir'] = file('config/quality')
}

findbugs {
    toolVersion = '3.0.1'
    ignoreFailures = true
    effort = 'default'
    reportLevel = 'medium'
    excludeFilter = file('config/quality/findbugs-exclude.xml')
}

task checkstyleHtml << {
    ant.xslt(in: checkstyleMain.reports.xml.destination,
             style: file('config/quality/checkstyle-noframes-sorted.xsl'),
             out: file("${checkstyleMain.reports.xml.destination.parent}/main.html"))
}

checkstyleMain.finalizedBy checkstyleHtml

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// Marker task to enable findbugs.
task findbugs(
    group: 'Verification',
    description: 'Marker task to enabled findbugs.'
)

gradle.taskGraph.whenReady { taskGraph ->
    tasks.findbugsMain.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
    tasks.findbugsTest.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
}



eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
    }
}
